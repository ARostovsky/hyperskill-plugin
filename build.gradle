buildscript {
    ext.pythonPluginVersion = pythonPluginVersion
    ext.pycharmSandbox = project.buildDir.absolutePath + File.separator + "pycharm-sandbox"
}

plugins {
    id "org.jetbrains.intellij" version "0.2.16"
    id "java"
    id("org.jetbrains.kotlin.jvm") version "1.1.4-2"
    id "idea"
}

repositories {
    mavenCentral()
}


dependencies {
    compile group: 'org.twitter4j', name: 'twitter4j-core', version: '4.0.1'
}


group = 'org.jetbrains.edu'
version = pluginVersion

sourceSets {
    main {
        java.srcDirs 'src', 'Edu-Java/src', 'Edu-Kotlin/src', 'Edu-Python/src'
        resources.srcDirs 'resources', 'Edu-Java/resources', 'Edu-Kotlin/resources', 'Edu-Python/resources'
    }

    test {
        java.srcDirs 'testSrc'
    }
}

intellij {
    if (project.hasProperty("ideaVersion")) {
        version ideaVersion
    } else if (project.hasProperty("ideaPath")) {
        localPath ideaPath
    }
    patchPluginXml.sinceBuild = '172.3544'
    updateSinceUntilBuild true
    downloadSources false
    plugins 'junit', 'Kotlin', "PythonCore:$pythonPluginVersion"
}

task configurePyCharmTask {
    doLast {
        if (!project.hasProperty("pycharmPath")) {
            throw new InvalidUserDataException("Path to PyCharm installed locally is needed\nDefine \"pycharmPath\" property")
        }
        intellij.sandboxDirectory pycharmSandbox
        intellij.alternativeIdePath pycharmPath
    }
}

task removePyCharm(type: Delete) {
    doLast {
        file(pycharmSandbox + File.separator + "plugins" + File.separator + "python-ce").deleteDir()
    }
}

// task for running pycharm with plugins locally
task runPyCharm {
    dependsOn configurePyCharmTask
    dependsOn runIde
}

// we need this so as not to install python plugin on PyCharm
prepareSandbox.finalizedBy(removePyCharm)

idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
        vcs = 'Git'
    }
}
